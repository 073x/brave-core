# Copyright (c) 2023 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/build/config.gni")
import("//build/toolchain/win/midl.gni")
import("//build/util/process_version.gni")
import("//chrome/process_version_rc_template.gni")

assert(is_win)

midl("brave_vpn_service_idl") {
  sources = [ "brave_vpn_service.idl" ]

  writes_tlb = true
}

# Must be in a config because of how GN orders flags (otherwise /W4 will appear
# after this, and turn it back on).
config("no-missing-braces") {
  # Macros invoked by WRL CoCreatableClass omit optional braces.
  cflags = [ "-Wno-missing-braces" ]
}

# This service needs to work with Windows 7, so explicitly setting the defines
# to reflect this. Otherwise, WRL uses APIs that are only available in later
# Windows versions.
config("winver") {
  defines = [
    "NTDDI_VERSION=NTDDI_WIN7",
    "_WIN32_WINNT=_WIN32_WINNT_WIN7",
    "WINVER=_WIN32_WINNT_WIN7",
  ]
}

source_set("common") {
  sources = [ "scoped_sc_handle.h" ]

  deps = [ "//base" ]
}

executable("brave_vpn_service") {
  configs -= [ "//build/config/win:winver" ]
  configs += [ ":winver" ]
  sources = [
    "brave_vpn_service.rc",
    "main.cc",
    "service_main.cc",
    "service_main.h",
    "service_utils.cc",
    "service_utils.h",
  ]

  deps = [
    ":common",
    ":version_resources",
    "//base",
    "//base:base_static",
    "//build/win:default_exe_manifest",
    "//chrome/install_static:install_static_util",
    "//components/version_info:channel",
  ]
  public_deps = [ ":brave_vpn_service_idl" ]
  libs = [
    "userenv.lib",  # For GetUserProfileDirectoryW()
  ]
}

process_version_rc_template("version_resources") {
  sources = [ "brave_vpn_service.ver" ]
  output = "$target_gen_dir/brave_vpn_service.rc"
}
