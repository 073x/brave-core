# Copyright (c) 2021 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# you can obtain one at http://mozilla.org/MPL/2.0/.

import("//brave/components/common/typescript.gni")
import("//chrome/common/features.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/grit/repack.gni")

preprocess_folder = "preprocessed"
preprocess_panel_manifest = "preprocessed_panel_manifest.json"
preprocess_page_manifest = "preprocessed_page_manifest.json"
preprocess_mojo_manifest = "preprocessed_mojo_manifest.json"

mojom("mojo_bindings") {
  sources = [ "wallet_ui.mojom" ]
  public_deps = [ "//mojo/public/mojom/base" ]
}

transpile_web_ui("deprecated_brave_wallet_page_ui") {
  entry_points = [ [
        "deprecated_brave_wallet_page",
        rebase_path("deprecated_page/deprecated_wallet_page.tsx"),
      ] ]
  resource_name = "deprecated_brave_wallet_page"
}

pack_web_resources("deprecated_brave_wallet_page_generated") {
  resource_name = "deprecated_brave_wallet_page"
  output_dir =
      "$root_gen_dir/brave/components/deprecated_brave_wallet_page/resources"
  deps = [ ":deprecated_brave_wallet_page_ui" ]
}

transpile_web_ui("brave_wallet_page_ui") {
  entry_points = [ [
        "brave_wallet_page",
        rebase_path("page/wallet_page.tsx"),
      ] ]
  resource_name = "brave_wallet_page"
  deps = [
    ":mojo_bindings_js",
    ":preprocess_mojo",
    ":preprocess_page",
    ":wallet_page_api_proxy",
  ]
}

pack_web_resources("brave_wallet_page_generated") {
  resource_name = "brave_wallet_page"
  output_dir = "$root_gen_dir/brave/components/brave_wallet_page/resources"
  deps = [ ":brave_wallet_page_ui" ]
}

transpile_web_ui("brave_wallet_panel_ui") {
  entry_points = [ [
        "brave_wallet_panel",
        rebase_path("panel/wallet_panel.tsx"),
      ] ]
  resource_name = "brave_wallet_panel"
  deps = [
    ":mojo_bindings_js",
    ":preprocess_mojo",
    ":preprocess_panel",
    ":wallet_panel_api_proxy",
  ]
}

pack_web_resources("brave_wallet_panel_generated") {
  resource_name = "brave_wallet_panel"
  output_dir = "$root_gen_dir/brave/components/brave_wallet_panel/resources"
  deps = [ ":brave_wallet_panel_ui" ]
}

repack("resources") {
  deps = [
    ":brave_wallet_page_generated",
    ":brave_wallet_panel_generated",
    ":deprecated_brave_wallet_page_generated",
  ]
  sources = [
    "$root_gen_dir/brave/components/brave_wallet_page/resources/brave_wallet_page_generated.pak",
    "$root_gen_dir/brave/components/brave_wallet_panel/resources/brave_wallet_panel_generated.pak",
    "$root_gen_dir/brave/components/deprecated_brave_wallet_page/resources/deprecated_brave_wallet_page_generated.pak",
  ]
  output =
      "$root_gen_dir/brave/components/brave_wallet/resources/brave_wallet.pak"
}

preprocess_if_expr("preprocess_panel") {
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_panel_manifest"
  in_files = [ "wallet_panel_api_proxy.js" ]
}

preprocess_if_expr("preprocess_page") {
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_page_manifest"
  in_files = [ "wallet_page_api_proxy.js" ]
}

preprocess_if_expr("preprocess_mojo") {
  deps = [ ":mojo_bindings_js" ]
  in_folder = "$target_gen_dir"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_mojo_manifest"
  in_files = [ "wallet_ui.mojom-lite.js" ]
}

js_library("wallet_panel_api_proxy") {
  deps = [
    ":mojo_bindings_js_library_for_compile",
    "//ui/webui/resources/js:cr.m",
    "//url/mojom:url_mojom_gurl_js_library_for_compile",
  ]
}

js_library("wallet_page_api_proxy") {
  deps = [
    ":mojo_bindings_js_library_for_compile",
    "//ui/webui/resources/js:cr.m",
    "//url/mojom:url_mojom_gurl_js_library_for_compile",
  ]
}
