import("//brave/build/config.gni")
import("//build/buildflag_header.gni")
import("//build/config/features.gni")
import("//chrome/common/features.gni")

import("//brave/components/greaselion/browser/buildflags/buildflags.gni")
import("//brave/components/brave_rewards/browser/buildflags/buildflags.gni")
import("//third_party/widevine/cdm/widevine.gni")
import("//brave/components/brave_wayback_machine/buildflags/buildflags.gni")
import("//brave/components/brave_perf_predictor/browser/buildflags/buildflags.gni")
import("//brave/components/speedreader/buildflags.gni")
import("//brave/components/tor/buildflags/buildflags.gni")
import("//brave/components/ipfs/buildflags/buildflags.gni")

buildflag_header("sparkle_buildflags") {
  header = "sparkle_buildflags.h"
  flags = [ "ENABLE_SPARKLE=$enable_sparkle" ]
}

source_set("version_info") {
  visibility = [
    ":*",
    "//brave/browser/*",
    "//chrome/browser",

    # these are laying violations and need to be fixed
    "//brave/components/greaselion/browser",
    "//brave/components/p3a",
  ]

  # these are only for version_info.cc and should not be exposed as public configs
  configs += [ "//brave/build:version" ]
  defines = [ "BRAVE_BROWSER_VERSION=\"$brave_version\"" ]

  sources = [
    "version_info.cc",
    "version_info.h",
  ]
}

source_set("tab_helpers") {
  sources = [
    "brave_tab_helpers.cc",
    "brave_tab_helpers.h",
  ]

  deps = [
    "//brave/browser/brave_ads:brave_ads",
    "//brave/browser/brave_stats:stats_updater",
    "//brave/browser/ephemeral_storage:ephemeral_storage",
    "//brave/browser/ui/bookmark:bookmark",
    "//brave/common:pref_names",
    "//brave/components/brave_rewards/browser/buildflags:buildflags",
    "//brave/components/brave_shields/browser:browser",
    "//brave/components/brave_shields/browser:browser",
    "//brave/components/brave_shields/browser/buildflags:buildflags",
    "//brave/components/brave_wayback_machine:buildflags",
    "//brave/components/greaselion/browser/buildflags:buildflags",
    "//brave/components/ipfs/buildflags:buildflags",
    "//brave/components/speedreader:buildflags",
    "//brave/components/tor/buildflags:buildflags",
    "//chrome/browser:browser",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui:ui",
    "//net:net",
    "//third_party/widevine/cdm:buildflags",
  ]

  if (enable_greaselion) {
    deps += [
      "//brave/browser/greaselion:greaselion",
    ]
  }

  if (brave_rewards_enabled) {
    deps += [
      "//brave/browser/brave_rewards:brave_rewards",
      "//brave/vendor/bat-native-ledger:headers",
    ]
  }

  if (enable_widevine) {
    sources += [
      "brave_drm_tab_helper.cc",
      "brave_drm_tab_helper.h",
    ]

    deps += [
      "//brave/browser/widevine",
      "//brave/components/brave_drm:brave_drm",
    ]
  }

  if (enable_brave_wayback_machine) {
    deps += [
      "//brave/browser/infobars:infobars"
    ]
  }

  if (enable_brave_perf_predictor) {
    deps += [
      "//brave/components/brave_perf_predictor/browser:browser",
    ]
  }

  if (enable_speedreader) {
    deps += [
      "//brave/browser/speedreader:speedreader",
    ]
  }

  if (enable_tor) {
    deps += [
      "//brave/components/tor:tor",
    ]
  }

  if (ipfs_enabled) {
    deps += [
      "//brave/browser/ipfs:ipfs",
    ]
  }
}

group("browser") {
  visibility = [
    "//brave:browser_dependencies",
    "//brave/browser/autoplay:browser_tests",
    "//brave/browser/farbling:browser_tests",
    "//brave/browser/ipfs/test:browsertests",
    "//brave/browser/ipfs/test:unittests",
    "//brave/browser/metrics/test:brave_metrics_unit_tests",
    "//brave/browser/new_tab:unittest",
    "//brave/browser/permissions:browser_tests",
    "//brave/browser/tor:browser_tests",
    "//brave/browser/tor:tor",
    "//brave/browser/widevine:browser_tests",
    "//brave/browser/widevine:browser_tests",
    "//brave/test:brave_browser_tests",
    "//brave/test:brave_unit_tests",
    "//brave/test:brave_unit_tests",

    # these are layering violation and need to be fixed
    "//brave/components/brave_ads/test:brave_ads_unit_tests",
    "//brave/components/brave_rewards/browser:testutil",
    "//brave/components/brave_rewards/test:brave_rewards_unit_tests",
    "//brave/components/ipfs/test:*",
  ]

  if (enable_sparkle) {
    deps += [ "//brave/vendor/sparkle:sparkle_framework_bundle_data" ]
  }

  # Group depending on upstream's counterpart to make it easier to manage
  # dependencies in brave/ (//chrome/browser does not depend on this target
  # so //brave/browser becomes essentially an alias for //chrome/browser now).
  public_deps = [ "//chrome/browser" ]
}
